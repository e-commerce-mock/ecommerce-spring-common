buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.owasp:dependency-check-gradle:5.2.1"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}

repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "https://mymavenrepo.com/repo/2w5k9sU2AsKfaYehyqno/" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api("com.ecommerce:ecommerce-shared-model:0.12-SNAPSHOT") { changing = true }

    api platform('org.springframework.boot:spring-boot-dependencies:2.1.4.RELEASE')


    //spring dependencies
    api('org.springframework.boot:spring-boot-starter-web')
    api("org.springframework.boot:spring-boot-starter-aop")
    api("org.springframework.boot:spring-boot-starter-jdbc")
    api("org.springframework.boot:spring-boot-starter-amqp")

    //common dependencies
    api("mysql:mysql-connector-java:8.0.15")
    api('io.jsonwebtoken:jjwt:0.9.1')
    api("org.flywaydb:flyway-core")
    api('com.google.guava:guava:27.0.1-jre')
    api('commons-io:commons-io:2.6')
    api('org.apache.commons:commons-lang3')
    api('org.apache.commons:commons-collections4:4.2')
    api('io.springfox:springfox-swagger2:2.9.2')
    api('io.springfox:springfox-swagger-ui:2.9.2')
    api('com.cwbase:logback-redis-appender:1.1.5')
    api('net.javacrumbs.shedlock:shedlock-spring:2.5.0')
    api('net.javacrumbs.shedlock:shedlock-provider-jdbc-template:2.5.0')


    //jackson dependencies
    api('com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.9')
    api('com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.9')
    api('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9')
    api('com.fasterxml.jackson.core:jackson-core:2.9.9')
    api('com.fasterxml.jackson.core:jackson-databind:2.9.9')
    api('com.fasterxml.jackson.core:jackson-annotations:2.9.9')


    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'


    //test dependencies
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation('org.junit.jupiter:junit-jupiter-engine')
    testImplementation('org.mockito:mockito-junit-jupiter:2.26.0')
}


configurations.all {
    exclude group: "junit", module: "junit"
    exclude group: "org.assertj", module: "assertj-core"

    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

tasks.withType(Test) {
    useJUnitPlatform()
}


wrapper {
    gradleVersion = '5.6'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.artifact
            version = rootProject.version
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }

    repositories {
        maven {
            url = "https://mymavenrepo.com/repo/Cd07WrKAtJ9Kq7PBaTuf/"
        }
    }
}


apply from: "${rootProject.projectDir}/gradle/spotbugs/spotbugs.gradle"
apply from: "${rootProject.projectDir}/gradle/checkstyle/checkstyle.gradle"
apply from: "${rootProject.projectDir}/gradle/git-hooks/git-hooks.gradle"
apply from: "${rootProject.projectDir}/gradle/version-info.gradle"
apply from: "${rootProject.projectDir}/gradle/jacoco.gradle"
